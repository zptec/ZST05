FUNCTION ZTMPTB_TABLE_RELATION.
*"----------------------------------------------------------------------
*"*"本地接口：
*"  TABLES
*"      IT_MAIN_RECORD_OLD OPTIONAL
*"      ET_MAIN_RECORD_NEW OPTIONAL
*"----------------------------------------------------------------------

"&---------------------------------------
"& ZST05_I06:/ TABLE RELATION ANALYZER
"& Boris Town
"&---------------------------------------

"&---------------------------------------
"& 01 Global Definition
"&---------------------------------------
  DATA REF_SQL_EXECUTOR TYPE REF TO CL_HDB_SQL_EXECUTOR.
  DATA REF_SYSTEM TYPE REF TO CL_DB6_SYS.
  DATA LT_ZTMPTB_MAIN_RECORD TYPE TABLE OF ZTMPTB_MAIN_RECORD.
  DATA LS_ZTMPTB_MAIN_RECORD TYPE ZTMPTB_MAIN_RECORD.
  DATA LT_ZTMPTB_MAIN_RECORD_OLD TYPE TABLE OF ST05_MAIN_RECORD.
  DATA: LS_ZTMPTB_MAIN_RECORD_OLD TYPE ST05_MAIN_RECORD,
        LR_RESULT_DESCR  TYPE REF TO CL_ABAP_STRUCTDESCR,
        LR_RESULT        TYPE REF TO DATA.
  DATA: L_TABIX TYPE I.
  DATA: L_SUB_TABIX TYPE I.
  CLEAR: REF_SQL_EXECUTOR, REF_SYSTEM, LT_ZTMPTB_MAIN_RECORD, LS_ZTMPTB_MAIN_RECORD,
  LT_ZTMPTB_MAIN_RECORD_OLD, LS_ZTMPTB_MAIN_RECORD_OLD, LR_RESULT_DESCR, LR_RESULT.
"&---------------------------------------
"& 02 Init SQL_EXECUTOR
"&---------------------------------------
  CREATE OBJECT REF_SQL_EXECUTOR.
  REF_SYSTEM = CL_DB6_SYS=>GET_SYS_REF( SYSTEM_ID     = SY-SYSID
                                        IGNORE_ERRORS = ''
                                        SYNCHRONIZE   = '' ).

  LT_ZTMPTB_MAIN_RECORD_OLD[] = IT_MAIN_RECORD_OLD[].

"&---------------------------------------
"& 03 Get statment and variable list
"&---------------------------------------
  TYPES: BEGIN OF LY_VAR_KEY,
          KEY TYPE ALPFSTRING,
          VAR TYPE CHAR255,
          LEN_VAR TYPE I,
        END OF LY_VAR_KEY.
  DATA: LTT_VAR_KEY TYPE TABLE OF LY_VAR_KEY.
  DATA: LS_VAR_KEY TYPE LY_VAR_KEY.

  TYPES: BEGIN OF LY_STATEMENT,
           SOURCE_INDEX TYPE I,
           OBJECT TYPE ST05_OBJECT,
           STATEMENT_WITH_NAMES TYPE ST05_STATEMENT,
           SELECT_SQL TYPE STRING,
           VARIABLES TYPE ALPF_STRING_TAB,
           KEYS TYPE ALPF_STRING_TAB,
           VAR_KEY LIKE LTT_VAR_KEY,
        END OF LY_STATEMENT.

  DATA: LT_STATEMENT TYPE TABLE OF LY_STATEMENT.
  DATA: LS_STATEMENT TYPE LY_STATEMENT.
  DATA: LS_STATEMENT_RELATION TYPE LY_STATEMENT.

"&---------------------------------------
"& 03.1 Get distinct objects
"&---------------------------------------
  DATA LT_OBJECT_LIST TYPE TABLE OF ST05_OBJECT.
  DATA LS_OBJECT_LIST TYPE ST05_OBJECT.
  DATA L_OBJECT_COUNT TYPE I.

  DATA L_VARIABLE_CURRENT TYPE STRING.
  DATA L_STR_SOURCEINDEX TYPE CHAR5.
  DATA L_STRLEN_FLD TYPE I.
  DATA VAR_INDEX TYPE I.
  DATA L_KEY TYPE ALPFSTRING.

  CLEAR: LT_OBJECT_LIST, LS_OBJECT_LIST, L_OBJECT_COUNT.

  LOOP AT LT_ZTMPTB_MAIN_RECORD_OLD INTO LS_ZTMPTB_MAIN_RECORD_OLD WHERE OBJECT IS NOT INITIAL.
    LS_STATEMENT-SOURCE_INDEX = SY-TABIX.
    LS_STATEMENT-OBJECT = LS_ZTMPTB_MAIN_RECORD_OLD-OBJECT.
    LS_STATEMENT-STATEMENT_WITH_NAMES = LS_ZTMPTB_MAIN_RECORD_OLD-STATEMENT_WITH_NAMES.

    READ TABLE LT_OBJECT_LIST INTO LS_OBJECT_LIST WITH KEY = LS_STATEMENT-OBJECT BINARY SEARCH.
    IF SY-SUBRC NE 0.
      ADD 1 TO L_OBJECT_COUNT.
      APPEND LS_STATEMENT-OBJECT TO LT_OBJECT_LIST.
      SORT LT_OBJECT_LIST.
    ENDIF.

    IF LS_ZTMPTB_MAIN_RECORD_OLD-STATEMENT_WITH_VALUES CP 'SELECT*'.
      CALL FUNCTION 'ZTMPTB_RESTORE_SQL'
        EXPORTING
          IV_STATEMENT            = LS_ZTMPTB_MAIN_RECORD_OLD-STATEMENT_WITH_NAMES
          IV_VARIABLES            = LS_ZTMPTB_MAIN_RECORD_OLD-VARIABLES
        IMPORTING
          EV_SQL                  = LS_STATEMENT-SELECT_SQL
          ET_VARIABLE_VALUE       = LS_STATEMENT-VARIABLES
          ET_KEYS                 = LS_STATEMENT-KEYS
        EXCEPTIONS
          REMARKS_NOT_MATCH       = 1
          OTHERS                  = 2
                .
      IF SY-SUBRC EQ 0.
        "ADD KEY VARIABLES TABLE
        LOOP AT LS_STATEMENT-KEYS INTO L_KEY.
          VAR_INDEX = SY-TABIX.
          LS_VAR_KEY-KEY = L_KEY.
          READ TABLE LS_STATEMENT-VARIABLES INTO L_VARIABLE_CURRENT INDEX VAR_INDEX.
          IF SY-SUBRC EQ 0.
            LS_VAR_KEY-VAR = L_VARIABLE_CURRENT.
            LS_VAR_KEY-LEN_VAR = STRLEN( L_VARIABLE_CURRENT ).
          ENDIF.
          APPEND LS_VAR_KEY TO LS_STATEMENT-VAR_KEY.
          CLEAR LS_VAR_KEY.
        ENDLOOP.
        "Search Longest string first
        SORT LS_STATEMENT-VAR_KEY BY LEN_VAR DESCENDING.
        APPEND LS_STATEMENT TO LT_STATEMENT.
        CLEAR LS_STATEMENT.
      ENDIF.
    ENDIF.
  ENDLOOP.
  SORT LT_STATEMENT BY SOURCE_INDEX.

"&---------------------------------------
"& 03.2 CREATE OBJECT RELATION MAPPING TABLE
"&---------------------------------------
  TYPES: BEGIN OF LST_MAP_ITEM ,
      FIELDNAME_L TYPE FIELDNAME, "LEFT TABLE MAP FIELD NAME
      FIELDNAME_R TYPE FIELDNAME, "RIGHT TABLE MAP FIELD NAME
      FIELDVALUE TYPE CHAR255, "MAP FIELD VALUE
    END OF LST_MAP_ITEM.
  DATA LT_MAP_ITEM TYPE TABLE OF LST_MAP_ITEM."MAP ITEMS
  DATA LS_MAP_ITEM TYPE LST_MAP_ITEM. "MAP ITEM
  TYPES: BEGIN OF LTT_RELATION_COL ,
          MAPS LIKE LT_MAP_ITEM, " MAP TABLE (2ed dimension)
*          MAP_STR TYPE ZTMPTB_MAIN_RECORD-ZZREMARK02, "MAPING STRING
        END OF LTT_RELATION_COL.
  DATA LS_RELATION_COL TYPE LTT_RELATION_COL.
  DATA LT_RELATION_COL TYPE TABLE OF LTT_RELATION_COL.
  DATA: BEGIN OF LT_RELATION_ROW OCCURS 1,
          COL LIKE LT_RELATION_COL,
        END OF LT_RELATION_ROW.
  DATA LS_RELATION_ROW LIKE LINE OF LT_RELATION_ROW.
  DATA L_RELATION_CELL TYPE ZTMPTB_MAIN_RECORD-ZZREMARK01.
  DATA DO_INDEX_RIGHT TYPE I.
  DATA DO_INDEX_LEFT TYPE I.
  CLEAR: DO_INDEX_RIGHT, L_RELATION_CELL.

  "TABLE RELATION STRING LIST
  DATA LT_MAP_STR_LIST TYPE TABLE OF ZTMPTB_MAIN_RECORD-ZZREMARK02.
  DATA L_MAP_STR TYPE ZTMPTB_MAIN_RECORD-ZZREMARK02.
  CLEAR: LT_MAP_STR_LIST[], L_MAP_STR.

  "RIGHT TABLE（1st dimension）
  DO L_OBJECT_COUNT TIMES.
    ADD 1 TO DO_INDEX_RIGHT.
    CLEAR DO_INDEX_LEFT.
    CLEAR LS_RELATION_ROW.
    "LEFT TABLE（2ed dimension）
    DO L_OBJECT_COUNT TIMES.
      ADD 1 TO DO_INDEX_LEFT.
      CLEAR LS_RELATION_COL.
*      CLEAR LT_MAP_ITEM.
      APPEND LS_RELATION_COL TO LS_RELATION_ROW-COL.
    ENDDO.
    APPEND L_MAP_STR TO LT_MAP_STR_LIST.
    APPEND LS_RELATION_ROW TO LT_RELATION_ROW.
    CLEAR L_MAP_STR.
    CLEAR LS_RELATION_ROW.
  ENDDO.

  "List of TableName,FieldName,FieldContent for binary search
  DATA: BEGIN OF LT_CONTENTS_FLOW OCCURS 1,
          TABLENAME LIKE LS_STATEMENT_RELATION-OBJECT,
          FIELDNAME TYPE FIELDNAME,
          STRLEN_FLD TYPE I,
          FIELDVALUE TYPE CHAR255,
          DATE  TYPE ZTMPTB_MAIN_RECORD-DATE ,
          TIME  TYPE ZTMPTB_MAIN_RECORD-TIME ,
        END OF LT_CONTENTS_FLOW.
  DATA LS_CONTENTS_FLOW LIKE LT_CONTENTS_FLOW.

  CLEAR: LT_CONTENTS_FLOW[], LS_CONTENTS_FLOW.

  LOOP AT LT_ZTMPTB_MAIN_RECORD_OLD INTO LS_ZTMPTB_MAIN_RECORD_OLD.
    L_TABIX = SY-TABIX.
    MOVE-CORRESPONDING LS_ZTMPTB_MAIN_RECORD_OLD TO LS_ZTMPTB_MAIN_RECORD.
    IF LS_ZTMPTB_MAIN_RECORD-STATEMENT_WITH_VALUES CP 'SELECT*'
      AND LS_ZTMPTB_MAIN_RECORD-NUMBER_OF_ROWS > 0.
      "CL_HDB_ACTION_SQL_EDITOR IF_DB6_ACTION_CONTROLLER~REFRESH_VIEW
      CLEAR LR_RESULT_DESCR.
      CLEAR LR_RESULT.
"&---------------------------------------
"& 04 Execute SQL Query
"&---------------------------------------
      READ TABLE LT_STATEMENT INTO LS_STATEMENT WITH KEY SOURCE_INDEX = L_TABIX BINARY SEARCH .
      IF SY-SUBRC EQ 0.
        L_SUB_TABIX = SY-SUBRC + 1.
        "EXECUTE QUERY
        TRY.
          REF_SQL_EXECUTOR->EXEC_QUERY_DYN( EXPORTING IM_STATEMENT    = LS_STATEMENT-SELECT_SQL
                                                  IM_CURSOR_SIZE  = '1'
                                                  IM_SYSTEM       = REF_SYSTEM
                                                  IM_CHECK        = ABAP_TRUE
                                                  IM_LOGGING      = ABAP_TRUE
                                        IMPORTING EX_STRUCTDESCR  = LR_RESULT_DESCR
                                                  EX_RESULT_REF   = LR_RESULT ).
        CATCH CX_ROOT INTO DATA(OREF).
          APPEND LS_ZTMPTB_MAIN_RECORD TO LT_ZTMPTB_MAIN_RECORD.
          CLEAR LS_ZTMPTB_MAIN_RECORD.
          "IF ERROR OCCURS IN SQL STATEMTNT, GOTO NEXT LINE
          CONTINUE.
        ENDTRY.
"&---------------------------------------
"& 05 Create receipt structure
"&---------------------------------------
        FIELD-SYMBOLS <LT_OUTTAB> TYPE STANDARD TABLE.
        FIELD-SYMBOLS <LS_OUTTAB> TYPE ANY.
        DATA LR_RESULT_LINE TYPE REF TO DATA.
        DATA LT_COMP_TAB TYPE CL_ABAP_STRUCTDESCR=>COMPONENT_TABLE.
        DATA LS_COMP_FLD TYPE CL_ABAP_STRUCTDESCR=>COMPONENT.

        UNASSIGN: <LT_OUTTAB>, <LS_OUTTAB>.

        CREATE DATA LR_RESULT_LINE TYPE HANDLE LR_RESULT_DESCR.
        ASSIGN LR_RESULT_LINE->* TO <LS_OUTTAB>.

        CLEAR LT_COMP_TAB.
        LT_COMP_TAB = LR_RESULT_DESCR->GET_COMPONENTS( ).
"&---------------------------------------
"& 06 Assign result table
"&---------------------------------------
        FIELD-SYMBOLS <L_FLD> TYPE ANY.
        DATA L_STR_FLD TYPE CHAR255.

        ASSIGN LR_RESULT->* TO <lt_outtab>.
"&---------------------------------------
"& 06.1 Table Relation Analysis
"&---------------------------------------
        "LOOP AT VARIABLE KEYS
        LOOP AT LS_STATEMENT-VAR_KEY INTO LS_VAR_KEY
          WHERE KEY NE 'MANDT'
          AND KEY NE 'CLIENT'
          AND VAR NE SY-MANDT.
*          VAR_INDEX = SY-TABIX.
          "SEARCH FOR MAPPING TABLE-FIELD
          READ TABLE LT_CONTENTS_FLOW INTO LS_CONTENTS_FLOW WITH KEY FIELDVALUE = LS_VAR_KEY-VAR BINARY SEARCH.
          IF SY-SUBRC EQ 0.
            L_TABIX = SY-TABIX.

            LOOP AT LT_CONTENTS_FLOW INTO LS_CONTENTS_FLOW FROM L_TABIX WHERE TABLENAME NE LS_ZTMPTB_MAIN_RECORD-OBJECT.
              "When no relation exist ,exit searching
              IF LS_CONTENTS_FLOW-FIELDVALUE NE LS_VAR_KEY-VAR.
                EXIT.
              ENDIF.

              "Concatenate relation text begin
              IF LS_ZTMPTB_MAIN_RECORD-ZZREMARK02 IS NOT INITIAL.
                CONCATENATE LS_ZTMPTB_MAIN_RECORD-ZZREMARK02 ';' INTO LS_ZTMPTB_MAIN_RECORD-ZZREMARK02.
              ENDIF.

              "Concatenate first relation text
              CONCATENATE LS_ZTMPTB_MAIN_RECORD-ZZREMARK02 LS_ZTMPTB_MAIN_RECORD-OBJECT '-'
              LS_VAR_KEY-KEY '=' INTO LS_ZTMPTB_MAIN_RECORD-ZZREMARK02.
              "Concatenate current relation text
              CONCATENATE LS_ZTMPTB_MAIN_RECORD-ZZREMARK02 LS_CONTENTS_FLOW-TABLENAME '-'
              LS_CONTENTS_FLOW-FIELDNAME '=' INTO LS_ZTMPTB_MAIN_RECORD-ZZREMARK02.
              "Concatenate relation text end
              CONCATENATE LS_ZTMPTB_MAIN_RECORD-ZZREMARK02 LS_VAR_KEY-VAR INTO LS_ZTMPTB_MAIN_RECORD-ZZREMARK02.
              CONDENSE LS_ZTMPTB_MAIN_RECORD-ZZREMARK02.
              EXIT.
            ENDLOOP.
          ENDIF.

        ENDLOOP.
"&---------------------------------------
"& 07 Find field relation
"&---------------------------------------
        "Get index of Left Table
        READ TABLE LT_OBJECT_LIST INTO LS_OBJECT_LIST WITH KEY = LS_STATEMENT-OBJECT BINARY SEARCH.
        IF SY-SUBRC EQ 0.
          DO_INDEX_RIGHT = SY-TABIX.
        ENDIF.
*        READ TABLE LT_RELATION_ROW INTO LS_RELATION_ROW.
        READ TABLE <LT_OUTTAB> INTO <LS_OUTTAB> INDEX 1.
        IF SY-SUBRC EQ 0.
          LOOP AT LT_COMP_TAB INTO LS_COMP_FLD
            WHERE NAME NE 'CLIENT'
            AND NAME NE 'MANDT'.
            ASSIGN COMPONENT LS_COMP_FLD-NAME OF STRUCTURE <LS_OUTTAB> TO <L_FLD>.
            WRITE <L_FLD> TO L_STR_FLD.
            CONDENSE L_STR_FLD.
            CHECK L_STR_FLD NE SY-MANDT.
            L_STRLEN_FLD = STRLEN( L_STR_FLD ).

            IF L_STRLEN_FLD > 1 AND L_STR_FLD CN '0 '.
              "Binary search For TableName/FieldName/FieldContent Content Flow
              LS_CONTENTS_FLOW-TABLENAME = LS_STATEMENT-OBJECT.
              LS_CONTENTS_FLOW-FIELDNAME = LS_COMP_FLD-NAME.
              LS_CONTENTS_FLOW-STRLEN_FLD = L_STRLEN_FLD.
              LS_CONTENTS_FLOW-FIELDVALUE = L_STR_FLD.
              LS_CONTENTS_FLOW-DATE = LS_ZTMPTB_MAIN_RECORD_OLD-DATE.
              LS_CONTENTS_FLOW-TIME = LS_ZTMPTB_MAIN_RECORD_OLD-TIME.
              APPEND LS_CONTENTS_FLOW TO LT_CONTENTS_FLOW.
              "Sort by string content for binary search
              SORT LT_CONTENTS_FLOW BY FIELDVALUE DATE TIME.

            ENDIF.
          ENDLOOP.
*          CONDENSE LS_ZTMPTB_MAIN_RECORD-ZZREMARK02.
"&---------------------------------------
"& 08 Display table line
"&---------------------------------------
          IF LS_ZTMPTB_MAIN_RECORD-ZZREMARK01 IS INITIAL.
            LOOP AT LT_COMP_TAB INTO LS_COMP_FLD.
              ASSIGN COMPONENT LS_COMP_FLD-NAME OF STRUCTURE <LS_OUTTAB> TO <L_FLD>.
              WRITE <L_FLD> TO L_STR_FLD.
              CONDENSE L_STR_FLD.
              IF LS_ZTMPTB_MAIN_RECORD-ZZREMARK01 IS INITIAL.
                LS_ZTMPTB_MAIN_RECORD-ZZREMARK01 =  L_STR_FLD.
              ELSE.
                CONCATENATE LS_ZTMPTB_MAIN_RECORD-ZZREMARK01 '|' L_STR_FLD
                INTO LS_ZTMPTB_MAIN_RECORD-ZZREMARK01 SEPARATED BY SPACE .
              ENDIF.
            ENDLOOP.
            CONDENSE LS_ZTMPTB_MAIN_RECORD-ZZREMARK01.
          ENDIF.
        ENDIF.
      ENDIF.
    ENDIF.
    APPEND LS_ZTMPTB_MAIN_RECORD TO LT_ZTMPTB_MAIN_RECORD.
    CLEAR LS_ZTMPTB_MAIN_RECORD.
  ENDLOOP.

FREE LT_RELATION_ROW.
ET_MAIN_RECORD_NEW[] = LT_ZTMPTB_MAIN_RECORD[].

ENDFUNCTION.
